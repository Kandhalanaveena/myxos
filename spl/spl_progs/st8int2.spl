alias physicalSP S0;
physicalSP= ([PTBR +2* (SP/512)] *512) +(SP%512);

alias sysCallNo S1;
sysCallNo=[physicalSP-1];
//print (sysCallNo);

if(sysCallNo==2) then
alias filename S2;
filename=[physicalSP-3];

    alias fat S3;
    fat=FAT;
    while(fat<DISK_LIST && [fat]!=filename) do
       fat=fat+8;
    endwhile;
    if(fat>=DISK_LIST) then
        [physicalSP-2]=-1;
        ireturn;
    endif;
    alias fatindex S4;
    fatindex=(fat-FAT)/8;


    alias swoft S5;
    swoft=FILE_TABLE;
    while(swoft<=1471 && [swoft]!=fatindex) do
       swoft=swoft+2;
    endwhile;
    alias swoftind S6;
    if(swoft<=1471) then
       swoftind=(swoft-FILE_TABLE)/2;
    else
       swoft=FILE_TABLE;
       while(swoft<=1471 && [swoft]!=-1) do
          swoft=swoft+2;
       endwhile;
       if(swoft>1471) then
          [physicalSP-2]=-1;
          ireturn;
       endif;
      
       swoftind=(swoft-FILE_TABLE)/2;

    endif;

       alias pid S7;
       pid=(PTBR-1024)/8;
       alias pcb S8;
       pcb=READY_LIST+pid*32;
       alias ppoft S9;
       ppoft=pcb+15;
       while(ppoft<=pcb+30 && [ppoft]!=-1) do
          ppoft=ppoft+2;
       endwhile;
       if(ppoft>pcb+30) then
         [physicalSP-2]=-1;
         ireturn; 
       endif;
         alias ppoftind S10;
         ppoftind=(ppoft-pcb-15)/2;
         [ppoft+0]=swoftind;
         [ppoft+1]=0;
         
         [swoft]=fatindex;
         [swoft+1]=[swoft+1]+1;
         [physicalSP-2]=ppoftind;

endif;  

if(sysCallNo==3) then
alias filedesc S2;
filedesc=[physicalSP-3];
//print(filedesc);

    if(filedesc<0 || filedesc>7) then
        [physicalSP-2]=-1;
        ireturn;
    endif;
       alias pid S3;
       pid=(PTBR-1024)/8;
       alias pcb S4;
       pcb=READY_LIST+pid*32;
       alias ppoft S5;
       ppoft=pcb+15;
       ppoft=ppoft+ (filedesc*2);
       if([ppoft]==-1) then
           [physicalSP-2]=-1;
           ireturn;
       endif;
          alias swoftind S6;
          swoftind=[ppoft];
          alias swoft S7;
          swoft=FILE_TABLE +(swoftind*2);
          [swoft+1]=[swoft+1]-1;
          if([swoft+1]==0) then
             [swoft]=-1;
          endif;
          [ppoft]=-1;
          [physicalSP-2]=0;
endif;
ireturn;    
       
    

